plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'maven-publish'
    id 'distribution'
}

group 'com.octopus.teamcity'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
        vendor = JvmVendorSpec.ADOPTOPENJDK
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply from: "${rootDir}/gradle/versions.gradle"

    // Provided configuration allows compile-time only dependencies to NOT
    // be included in the final distributions.
    configurations {
        provided
    }

    sourceSets {
        all {
            compileClasspath += configurations.provided
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    version = rootProject.version
    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
        maven { url "https://download.jetbrains.com/teamcity-repository" }
        maven { url "https://packages.jetbrains.team/maven/p/teamcity-rest-client/teamcity-rest-client" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        implementation 'org.apache.logging.log4j:log4j-api'
        implementation 'org.apache.logging.log4j:log4j-core'
        implementation 'org.apache.logging.log4j:log4j-slf4j-impl'
    }
}

apply plugin: "distribution"

distributions {
    main {
        distributionBaseName = "Octopus.TeamCity.OpenTelemetry." + rootProject.version
        version = ''
        contents {
            from file("teamcity-plugin.xml"), {
                filter(org.apache.tools.ant.filters.ReplaceTokens, tokens:[version: rootProject.version])
            }
            into('server/') {
                from tasks.findByPath(":server:installDist")
            }
            into '/'
        }
    }
}
